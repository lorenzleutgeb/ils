\hypertarget{hakuna-matata-the-wumpus-hunter}{%
\section{Hakuna Matata the Wumpus
Hunter}\label{hakuna-matata-the-wumpus-hunter}}

This is an implementation of an agent that plays ``Hunt the Wumpus''.

\hypertarget{constants}{%
\subsection{Constants}\label{constants}}

We use constants to encode concrete orientations, actions and modes, and
one corresponding predicate to represent all of them respectively.

According to \texttt{wumpus.common.Orientation} and
\texttt{.../wsu/Orientation.py} we define orientations.

\begin{verbatim}
#const right = 0.
#const up    = 1.
#const left  = 2.
#const down  = 3.

isOrientation 0..3
\end{verbatim}

According to \texttt{wumpus.common.Action} and
\texttt{.../wsu/Action.py} we define actions.

\begin{verbatim}
#const goforward = 0.
#const turnleft  = 1.
#const turnright = 2.
#const grab      = 3.
#const shoot     = 4.
#const climb     = 5.

isAction 0..5
\end{verbatim}

According to \texttt{wumpus.agent.Mode} (not present in
\texttt{.../wsu}) we define modes.

\begin{verbatim}
#const explore = 0.
#const escape  = 1.
#const kill    = 2.

isMode 0..3
\end{verbatim}

Additionally we designate two orthogonal orientations as axes.

\begin{verbatim}
axis right
axis up
\end{verbatim}

Being able to mirror orientations/turns will come in handy.

\begin{verbatim}
mirror         left right
mirror         up   down
\end{verbatim}

And of course, our mirror is symmetric :)

\begin{verbatim}
mirror         O2   O1
        mirror O1   O2
\end{verbatim}

\hypertarget{world-size}{%
\subsection{World Size}\label{world-size}}

If the agent never bumped against a wall, it has no information about
the size of the world.

\begin{verbatim}
sizeKnown
        bumped _ _
\end{verbatim}

We will therefore derive the size of the world from something already
known. We derive the maximum coordinate we have seen from the explored
rooms we know about.

\begin{verbatim}
exploredSize     X
        explored X Y

exploredSize       Y
        explored X Y
\end{verbatim}

Since any room we already explored certainly is part of the world, we
can use this information and guess that the world is ``just a little''
bigger than we already know.

\begin{verbatim}
size                            S
        not sizeKnown
        #succ             Sm    S
        #max              Sm Se
                exploredSize Se
\end{verbatim}

Of course, if the agent bumped, the size can be directly inferred.

\begin{verbatim}
size               S
        bumped  Sm Y
        >     Sm Y
        #prec Sm   S

size               S
        bumped  X Sm
        <=    X Sm
        #prec   Sm S
\end{verbatim}

\hypertarget{rooms-neighbors-facing-and-bumps}{%
\subsection{Rooms, Neighbors, Facing and
Bumps}\label{rooms-neighbors-facing-and-bumps}}

Span the space of rooms.

\begin{verbatim}
room         X Y
        #int X
        #int   Y
        >      Y 0
        >    X   0
        <=     Y S
        <=   X   S
        size     S

corner 1 1

corner       X 1
        room X 1
        size X
        sizeKnown

corner       1 Y
        room 1 Y
        size   Y
        sizeKnown

corner       S S
        room S S
        size S
        sizeKnown
\end{verbatim}

Two rooms are different if any of the components X, Y are unequal.

\begin{verbatim}
diff         X1 Y1 X2 Y2
        room X1 Y1
        room       X2 Y2
        !=   X1    X2

diff         X1 Y1 X2 Y2
        room X1 Y1
        room       X2 Y2
        !=      Y1    Y2
\end{verbatim}

\hypertarget{distances-and-costs}{%
\subsubsection{Distances and Costs}\label{distances-and-costs}}

The two predicates \texttt{outgoing/2} and \texttt{incoming/2} will help
us to keep the space for which we have to compute costs small.

\begin{verbatim}
neighborhood              X2 Y2
        now         X1 Y1       _
        anyNeighbor X1 Y1 X2 Y2
        safe              X2 Y2

outgoing             X Y
        neighborhood X Y

outgoing     X Y
        now  X Y _

incoming     X Y
        safe X Y
\end{verbatim}

We define distance along axes

\begin{verbatim}
distAlong        X1 Y1 X2 Y2 right D
        room     X1 Y1
        room           X2 Y2
        #absdiff       X1 X2       D

distAlong        X1 Y1 X2 Y2 up D
        #absdiff       Y1 Y2    D
        room     X1 Y1
        room           X2 Y2
\end{verbatim}

And using that, manhattan/taxicab distance is a charm.

\begin{verbatim}
manhattan         X1 Y1 X2 Y2             M
        outgoing  X1 Y1
        distAlong X1 Y1 X2 Y2 right D1
        distAlong X1 Y1 X2 Y2 up       D2
        +                           D1 D2 M
\end{verbatim}

The turn predicate associates two orientations with the action that must
be taken in order to turn from the first orientation to the second (if
possible).

\begin{verbatim}
turn up    left  turnleft
turn left  down  turnleft
turn down  right turnleft
turn right up    turnleft

turn up    right turnright
turn down  left  turnright
turn left  up    turnright
turn right down  turnright
\end{verbatim}

From here, we abstract towards a cost function. We are interested in the
number of turns that need to be performed in order to change from one
orientation to another.

Of course, no change in orientation is free.

\begin{verbatim}
turns                 O O 0
        isOrientation O
\end{verbatim}

Left and right turns, i.e.~ones that do not mirror orientation take
exactly one turn.

\begin{verbatim}
turns                     O1 O2 1
            isOrientation O1
            isOrientation    O2
            !=            O1 O2
        not mirror        O1 O2
\end{verbatim}

The worst case is when the orientations are mirrored.

\begin{verbatim}
turns                  O1 O2 2
        isOrientation  O1
        isOrientation     O2
        mirror         O1 O2
\end{verbatim}

With this simple counts for turns we go further and compute how many
turns are needed along the way from one room to another.

Again, staying in the same room is free.

\begin{verbatim}
turnsTo               X Y O X Y 0
        outgoing      X Y
        isOrientation     O
\end{verbatim}

Rooms that are faced also need no turns, one can just go straight
forward.

\begin{verbatim}
turnsTo               X1 Y1 O1 X2 Y2 0
        isOrientation       O1
        facing        X1 Y1 _  X2 Y2
        diff          X1 Y1    X2 Y2
\end{verbatim}

All other cases require at most one turn since two components are off.

\begin{verbatim}
turnsTo               X1 Y1 O1 X2 Y2 1
        outgoing      X1 Y1
        isOrientation       O1
        incoming               X2 Y2
        not turnsTo   X1 Y1 O1 X2 Y2 0

turnsFrom                X1 Y1 O1 X2 Y2    C
        outgoing         X1 Y1
        isOrientation          O1
        incoming                  X2 Y2
        #min                               C Cm
                reach    X1 Y1    X2 Y2 O2
                turns          O1       O2   Cm

turnCost              X1 Y1 O1 X2 Y2       C
        outgoing      X1 Y1
        isOrientation       O1
        incoming               X2 Y2
        turnsTo       X1 Y1 O1 X2 Y2 Cp
        turnsFrom     X1 Y1 O1 X2 Y2    Cd
        +                            Cp Cd C
\end{verbatim}

We combine turn cost and Manhattan distance as high level cost function.

\begin{verbatim}
cost                      X1 Y1 O1 X2 Y2       C3
        outgoing          X1 Y1
        isOrientation           O1
        incoming                   X2 Y2
        turnCost          X1 Y1 O1 X2 Y2 C1
        manhattan         X1 Y1    X2 Y2    C2
        +                                C1 C2 C3
\end{verbatim}

Neighboring rooms along the horizontal and vertical axis.

\begin{verbatim}
neighbor      X1 Y X2 Y right
        room  X1 Y
        room       X2 Y
        #succ X1   X2

neighbor      X Y1 X Y2 up
        room  X Y1
        room       X Y2
        #succ   Y1   Y2

neighbor         X1 Y1 X2 Y2 O2
        neighbor X2 Y2 X1 Y1 O1
        mirror               O1 O2

anyNeighbor           X1 Y1 X2 Y2
        neighbor      X1 Y1 X2 Y2 O
        isOrientation             O

twoNeighbors        X1 Y1 X2 Y2 X3 Y3
        anyNeighbor X1 Y1 X2 Y2
        anyNeighbor X1 Y1       X3 Y3
        diff              X2 Y2 X3 Y3

square               X1 Y1 X2 Y2 X3 Y3 X4 Y4
        twoNeighbors X1 Y1 X2 Y2 X3 Y3
        twoNeighbors X4 Y4 X2 Y2 X3 Y3
        diff         X1 Y1             X4 Y4

triple           X1 Y1 X2 Y2 X3 Y3
        neighbor X1 Y1 X2 Y2       A
        neighbor       X2 Y2 X3 Y3 A
        diff     X1 Y1       X3 Y3
        axis                       A
\end{verbatim}

Rooms that agree on one component.

\begin{verbatim}
facing       X Z up X Y
        room X Z
        <      Z      Y
        room X        Y

facing       Z Y right X Y
        room           X Y
        <    Z         X
        room Z           Y

facing         X2 Y2    O2 X1 Y1
        facing X1 Y1 O1    X2 Y2
        mirror       O1 O2

reach        X1 Y1 X2 Y2 right
        room X1 Y1
        room       X2 Y2
        <    X1    X2

reach        X1 Y1 X2 Y2 up
        room X1 Y1
        room       X2 Y2
        <       Y1    Y2

reach        X1 Y1 X2 Y2 left
        room X1 Y1
        room       X2 Y2
        >    X1    X2

reach        X1 Y1 X2 Y2 down
        room X1 Y1
        room       X2 Y2
        >       Y1    Y2
\end{verbatim}

Complete information about information. We only do this here and not in
Python since we might have assumed a size.

\begin{verbatim}
-explored            X Y
        room         X Y
        not explored X Y
\end{verbatim}

\hypertarget{do}{%
\subsection{Do}\label{do}}

\begin{verbatim}
priority
        shouldShoot

priority
        shouldClimb

priority
        shouldGrab

priority
        shouldAim

do shoot
        shouldShoot

do grab
        shouldGrab

do climb
        shouldClimb

do              A
        succAim A

do                   A
            towards  A
        not priority

shouldShoot
        mode   kill
        now    X Y O
        attack X Y O
\end{verbatim}

We restrict ourselves to only grab once.

\begin{verbatim}
-canGrab
        grabbed _ _

canGrab
        not -canGrab
\end{verbatim}

Pick gold if there's some in the room, independently of the current
mode.

\begin{verbatim}
shouldGrab
        now     X Y O
        glitter X Y
        canGrab
\end{verbatim}

Climb if gold is picked and back to initial room.

\begin{verbatim}
shouldClimb
        now 1 1 O
        mode escape
\end{verbatim}

Generally we will be turning and go forward towards a goal. Exceptions
are when we want to take high priority actions like grabbing, climbing
or shooting.

\begin{verbatim}
canFace                          A
        facing X1 Y1    O2 X2 Y2
        now    X1 Y1 O1
        next               X2 Y2
        !=           O1 O2
        turn         O1 O2       A

-cannotFace
        isAction A
        canFace  A

cannotFace
        not -cannotFace

towards goforward
        facing X1 Y1 O1 X2 Y2
        now    X1 Y1 O1
        next            X2 Y2

towards         A
        canFace A
        not towards goforward
\end{verbatim}

If the agent cannot go forward towards next and also cannot face it by
turning once it needs to turn around. We currently implement this as
left turns always. There might be better ways to sort this out\ldots{}

\begin{verbatim}
towards turnleft
        not towards goforward
        cannotFace
\end{verbatim}

\hypertarget{detection-of-wumpus}{%
\subsection{Detection Of Wumpus}\label{detection-of-wumpus}}

\begin{verbatim}
wumpus            X1 Y1
         square   X1 Y1 X2 Y2 X3 Y3 X4 Y4
         stench         X2 Y2
         stench               X3 Y3
         explored                   X4 Y4
        -killed
\end{verbatim}

Antipodal matching.

\begin{verbatim}
wumpus                X2 Y2
         triple X1 Y1 X2 Y2 X3 Y3
         stench X1 Y1
         stench             X3 Y3
        -killed
\end{verbatim}

Corner case.

\begin{verbatim}
wumpus                      XW YW
         twoNeighbors XC YC XW YW XE YE
         stench       XC YC
         corner       XC YC
         explored                 XE YE
        -killed
\end{verbatim}

Auxiliary flag to signal detection of wumpus.

\begin{verbatim}
wumpusDetected
        room   X Y
        wumpus X Y
\end{verbatim}

If wumpus is not certainly detected, we must exclude other rooms where
it might be.

\begin{verbatim}
possibleWumpus                X2 Y2
            anyNeighbor X1 Y1 X2 Y2
            -explored         X2 Y2
            stench      X1 Y1
        not shotAt            X2 Y2
            -killed
        not wumpusDetected

shotAt                  X Y
        facing XS YS OS X Y
        shot   XS YS OS
\end{verbatim}

\hypertarget{detection-of-pits}{%
\subsection{Detection Of Pits}\label{detection-of-pits}}

A neighbor of a breeze is a possible pit if it can be.

\begin{verbatim}
possiblePit         XB YB XP YP
        breeze      XB YB
        anyNeighbor XB YB XP YP
        not -pit          XP YP
\end{verbatim}

Any explored room certainly cannot be a pit, otherwise we would be dead
by now.

\begin{verbatim}
-pit             X Y
        explored X Y
\end{verbatim}

If we have explored a room already and felt no brezee, then no neighbor
can be a pit.

\begin{verbatim}
-pit                      X2 Y2
        anyNeighbor X1 Y1 X2 Y2
        -breeze     X1 Y1
\end{verbatim}

Finally, we assume that there are pits where there might be possible
pits.

\begin{verbatim}
pit                     X Y
        possiblePit _ _ X Y
\end{verbatim}

\hypertarget{safety-of-rooms}{%
\subsection{Safety Of Rooms}\label{safety-of-rooms}}

Any room where the wumpus is is not safe.

\begin{verbatim}
safe             X Y
        explored X Y

-safe          X Y
        wumpus X Y

-safe                  X Y
        possibleWumpus X Y

-safe       X Y
        pit X Y

safe                   X Y
             reachable X Y
        not -safe      X Y
\end{verbatim}

\hypertarget{explore-mode}{%
\subsection{Explore Mode}\label{explore-mode}}

\begin{verbatim}
reachable           X Y
        anyNeighbor X Y XE YE
        explored        XE YE

reachable        X Y
        explored X Y
\end{verbatim}

Auxiliary flag to signal whether we can still explore further.

\begin{verbatim}
frontier           X Y
         reachable X Y
         safe      X Y
        -explored  X Y

shouldExplore
        frontier X Y
\end{verbatim}

\hypertarget{kill-mode}{%
\subsection{Kill Mode}\label{kill-mode}}

This mode is concerned with killing, or trying to kill, the wumpus.
Killing the wumpus makes sense if this allows to further explore the
world. Trying to kill it may still allow us to infer where the wumpus
is, even though it might still be alive.

First, we identify cases where we certainly should not (attempt to) kill
the wumpus.

If the wumpus is in a room where there might also be a pit, then we will
not be able to pass safely even if we manage to kill it.

\begin{verbatim}
dontShoot
        wumpus X Y
        pit    X Y
\end{verbatim}

The same holds for uncertainty of the wumpus' location. Note that this
constraint may be relaxed, since in the case we miss the wumpus, we
might deduce that we can pass through another room.

\begin{verbatim}
dontShoot
        possibleWumpus X Y
        pit            X Y
\end{verbatim}

Certainly, we cannot (and will not) shoot if we already used our arrow.

\begin{verbatim}
dontShoot
        shot _ _ _
\end{verbatim}

Also, if the wumpus is already killed, we will not shoot again.

\begin{verbatim}
dontShoot
        killed
\end{verbatim}

If we already have the gold, we should be on our way out of the cave,
and leave the wumpus alone.

\begin{verbatim}
dontShoot
        grabbed _ _
\end{verbatim}

We can kill the wumpus if we know where it is, and there is a safe room
from which it can be faced.

\begin{verbatim}
canKill        XS YS OS
        facing XS YS OS XW YW
        wumpus          XW YW
        safe   XS YS
\end{verbatim}

We should actually kill the wumpus if it is possible, and if none of our
special cases above tells us not to.

\begin{verbatim}
shouldKill
            canKill   _ _ _
        not dontShoot
\end{verbatim}

Analogously, we might \emph{try} to kill it.

\begin{verbatim}
canTryKill       X Y O
        facing   X Y O XC YC
        safe     X Y
        possibleWumpus XC YC

shouldTryKill
            canTryKill _ _ _
        not shouldKill
        not dontShoot
\end{verbatim}

We generalize from trying to kill and actually killing to an attack,
which streamlines the actions that need to be done in both cases.

\begin{verbatim}
attack                X Y O
        canTryKill    X Y O
        shouldTryKill

attack             X Y O
        canKill    X Y O
        shouldKill

shouldAttack
        attack _ _ _

aim                        OS
            !=           O OS
            attack XS YS   OS
        not attack XS YS O
            now    XS YS O

canAim                  A
        turn      O1 O2 A
        aim          O2
        now   _ _ O1

-cannotAim
        canAim _

cannotAim
        not -cannotAim

succAim        A
        canAim A
        mode   kill

succAim           turnleft
        aim       _
        cannotAim
        mode      kill

shouldAim
        succAim A
\end{verbatim}

\hypertarget{a-search}{%
\subsection{A*-Search}\label{a-search}}

This section is concerned with selection of the goal room, and
consequently the next room to move to.

We will use weak constraints in two levels. The first level is concerned
with finding out the optimal goal while the second level is then about
choosing the next room.

\begin{verbatim}
level 1
level 2
\end{verbatim}

\hypertarget{candidate-rooms}{%
\subsubsection{Candidate Rooms}\label{candidate-rooms}}

For both levels we will define candidate sets in the following
subsections.

\hypertarget{goal-room}{%
\paragraph{Goal Room}\label{goal-room}}

For convenience we define a selection on the cost function that fixes
the current location and orientation of the agent.

\begin{verbatim}
costFromNow           X2 Y2 C
        cost X1 Y1 O1 X2 Y2 C
        now  X1 Y1 O1
\end{verbatim}

Now, for the three modes that require goals, we define candidate sets.

In the explore mode, interesting candidates are those rooms that we have
not yet explored and for which we know that they are safe.

\begin{verbatim}
candidate 1         X Y C
        costFromNow X Y C
        frontier    X Y
        mode        explore
\end{verbatim}

The goal is a room from which we can attack the wumpus.

\begin{verbatim}
candidate 1         X Y C
        costFromNow X Y C
        attack      X Y _
        mode        kill
\end{verbatim}

When escaping, the only candidate is the starting point.

\begin{verbatim}
candidate 1 1 1 1
        mode escape
\end{verbatim}

\hypertarget{next-room}{%
\paragraph{Next Room}\label{next-room}}

Choosing the next room is trivial if the goal room is adjacent to the
one that the agent is currently in: Just move there.

\begin{verbatim}
easyGoal
        now         XN YN       _
        anyNeighbor XN YN XG YG
        goal              XG YG

candidate 2      X Y C
        choice 1 X Y C
        easyGoal
\end{verbatim}

Otherwise, choose an adjacent room.

\begin{verbatim}
candidate 2                      X2 Y2                C
            now         X1 Y1 O1
            anyNeighbor X1 Y1    X2 Y2
            reach       X1 Y1    X2 Y2 O2
            safe                 X2 Y2
            cost                 X2 Y2 O2 XG YG    C2
            goal                          XG YG
            turnsFrom   X1 Y1 O1 X2 Y2          C1
            +                                   C1 C2 C
        not easyGoal
\end{verbatim}

\hypertarget{weak-constraints}{%
\subsubsection{Weak Constraints}\label{weak-constraints}}

Now, any candidate is either our choice or it is not. Span the search
space.

\begin{verbatim}
choice L X Y C | -choice L X Y C
        level     L
        candidate L X Y C
\end{verbatim}

Of course we want to minimize cost.

\begin{verbatim}
~
        level     L
        candidate L X1 Y1 C1
        candidate L          X2 Y2 C2
        choice    L          X2 Y2 C2
        <=                C1       C2
\end{verbatim}

Give more explicit names to the choices.

\begin{verbatim}
next X Y
        choice 2 X Y C

goal X Y
        choice 1 X Y C

foundNext
        next _ _

-
            foundGoal
        not foundNext

foundGoal
        goal _ _

-
        not foundGoal
        not priority
\end{verbatim}

\hypertarget{autopilot}{%
\subsection{Autopilot}\label{autopilot}}

\begin{verbatim}
-autopilot
        not autopilot

autopilot
        not priority
        mode explore

autopilot
        not priority
        mode escape
\end{verbatim}

\hypertarget{mode-selector}{%
\subsection{Mode Selector}\label{mode-selector}}

\begin{verbatim}
mode grab
        shouldGrab

mode explore
            shouldExplore
            canGrab
        not mode grab

mode kill
            canGrab
            shouldAttack
        not mode         grab
        not mode         explore

mode escape
        not mode grab
        not mode explore
        not mode kill
\end{verbatim}